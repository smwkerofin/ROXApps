dnl Process this file with autoconf to produce a configure script.
AC_INIT(pkg.c)
AC_CONFIG_HEADER(rox-clib.h)

AC_ARG_WITH(platform,
[  --with-platform	  platform name, as detected by AppRun],
[AC_SUBST(with_platform)],
[
AC_MSG_ERROR([

Please do not run 'configure' manually - instead, run the AppRun script with
the --compile option, like this:
	$ ROX-CLib/AppRun --compile

Thank you.
])
])

AC_ARG_ENABLE(shlib, 
  --enable-shlib  Build shared library (yes),
 SHLIB=$enable_val, SHLIB=yes)
AC_SUBST(SHLIB)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_LIBTOOL

if test -z "$PKG_CONFIG"; then
  PKG_CONFIG=pkg-config
fi
AC_SUBST(PKG_CONFIG)
AC_DEFINE_UNQUOTED(PKG_CONFIG, "$PKG_CONFIG")

AC_MSG_CHECKING(whether we are using Zero Install to get the headers)
dnl changequote(,)dnl
if test "$GTKHEADERS" != "" ; then
  dnl We're using 0launch on the source interface; correct for new .pc file locations
  PKG_CONFIG_FLAGS="--define-variable=gtkprefix=\"$GTKHEADERS\" --define-variable=glibprefix=\"$GLIBHEADERS\" --define-variable=pangoprefix=\"$PANGOHEADERS\""
  AC_MSG_RESULT(yes; GTK headers are in $GTKHEADERS)
else
  PKG_CONFIG_FLAGS=
  AC_MSG_RESULT(no)
fi
dnl changequote([,])dnl

AC_DEFINE(GTK2)
gtk2=1
GTK_CFLAGS="$PKG_CONFIG $PKG_CONFIG_FLAGS gtk+-2.0 --cflags"
GTK_LIBS="$PKG_CONFIG $PKG_CONFIG_FLAGS gtk+-2.0 --libs"

AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)
AC_DEFINE_UNQUOTED(GTK_CFLAGS, "$GTK_CFLAGS")
AC_DEFINE_UNQUOTED(GTK_LIBS, "$GTK_LIBS")

AC_MSG_CHECKING(that pkg-config runs)
if "$PKG_CONFIG" --version > /dev/null 2>&1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([*** $PKG_CONFIG missing ***

\'$PKG_CONFIG --version\' failed to run - make sure it is inside one of the
directories in your PATH environment variable!

pkg-config comes with the developement packages for GTK+-2.0, available at:

    http://www.gtk.org])
fi

AC_DEFUN(ROX_REQUIRE, [
AC_MSG_CHECKING(that $1 (version >= $2) is installed)
if "$PKG_CONFIG" $PKG_CONFIG_FLAGS --atleast-version=$2 $1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  if "$PKG_CONFIG" $PKG_CONFIG_FLAGS --exists $1 ; then
    AC_MSG_ERROR(Current version is only `"$PKG_CONFIG" $PKG_CONFIG_FLAGS "$1" --modversion`)
  else
    AC_MSG_ERROR(Package is not installed)
  fi
fi
])

ROX_REQUIRE(gtk+-2.0, 2.0.1)
ROX_REQUIRE(glib-2.0, 2.0.3)
ROX_REQUIRE(libxml-2.0, 2.4.0)

XML_CONFIG="$PKG_CONFIG $PKG_CONFIG_FLAGS libxml-2.0"
AC_SUBST(XML_CONFIG)

dnl Checks for libraries.
AC_CHECK_LIB(nsl, gethostbyname)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(apsymbols.h apbuild/apsymbols.h libintl.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Checks for library functions.
AC_CHECK_FUNCS(mkdir gethostname gethostbyname dgettext bindtextdomain)

dnl Extract version info from AppInfo.xml
AC_MSG_CHECKING(version information)
changequote(,)dnl
ROXCLIB_VERSION=`sed -n 's/^.*<Version>\([^<]*\)<\/Version>.*$/\1/p' ../AppInfo.xml`
ROXCLIB_VERSION_NUM=`sed -n 's/^.*<Version>\([.0-9]*\).*<\/Version>.*$/\1/p' ../AppInfo.xml`
changequote([,])dnl
AC_DEFINE_UNQUOTED(ROXCLIB_VERSION, "$ROXCLIB_VERSION")
AC_SUBST(ROXCLIB_VERSION_NUM)
AC_MSG_RESULT(version $ROXCLIB_VERSION)

LIBNAME=rox-clib
AC_SUBST(LIBNAME)

AC_OUTPUT(Makefile)
