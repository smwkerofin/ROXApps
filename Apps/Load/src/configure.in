dnl Process this file with autoconf to produce a configure script.
AC_INIT(load.c)
AC_CONFIG_HEADER(config.h)

AC_ARG_WITH(platform,
[  --with-platform	  platform name, as detected by AppRun],
[AC_SUBST(with_platform)],
[
AC_MSG_ERROR([

Please do not run 'configure' manually - instead, run the AppRun script with
the --compile option, like this:
	$ Load/AppRun --compile

Thank you.
])
])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET

if test -z "$GTK_CONFIG"; then
  GTK_CONFIG=gtk-config
fi
AC_SUBST(GTK_CONFIG)

if test -z "$ROX_RUN"; then
  AC_CHECK_PROG(ROX_RUN, rox_run, rox_run, "$APP_DIR/rox_run")
fi
AC_SUBST(ROX_RUN)

dnl Make sure we have libgtop
LIBGTOP_LIBS=`echo \`libgtop-config --libs --extra-libs\``
LIBGTOP_EXTRA_LIBS=`libgtop-config --extra-libs`

AC_MSG_CHECKING(for libgtop library)
if test -n "$LIBGTOP_LIBS" ; then
  AC_MSG_RESULT(yes - and with new style config)
  LIBGTOP_INCS=`libgtop-config --cflags`
else
  GTOP_CONFIG=`libgtop-config --config`
  if test -n "$GTOP_CONFIG" ; then
    AC_MSG_RESULT(yes - using '$GTOP_CONFIG')
  else
    AC_MSG_ERROR(can't run 'libgtop-config --config' - \
    		make sure it is in your PATH)
  fi
  source "$GTOP_CONFIG"
fi
AC_SUBST(LIBGTOP_EXTRA_LIBS)
AC_SUBST(LIBGTOP_LIBS)
AC_SUBST(LIBGTOP_INCS)

AC_CHECK_PROG(XML_CONFIG, xml2-config, xml2-config, false)
dnl AC_DEFINE_UNQUOTED(XML_CONFIG, "$XML_CONFIG")
if test "$XML_CONFIG" != "false"; then
  AC_DEFINE(HAVE_XML)
fi

dnl Checks for header files.
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(locale.h libintl.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(gethostname setlocale bindtextdomain gettext)

dnl Check for ROX-CLib and version
AC_MSG_CHECKING(ROX-CLib)
if "$ROX_RUN" ROX-CLib ; then
  verstr=`"$ROX_RUN" ROX-CLib -v`
  ver=`echo $verstr | cut -d " " -f 2`
  changequote(,)dnl
  ROX_CLIB_VERSION=`echo $ver | sed -e "s/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\)/10000*\1+100*\2+\3/" | bc`
  changequote([,])dnl
  AC_DEFINE_UNQUOTED(ROX_CLIB_VERSION, $ROX_CLIB_VERSION)
  AC_MSG_RESULT(version $ver)
  if test $ROX_CLIB_VERSION -lt 202 ; then
    AC_MSG_ERROR(ROX-CLib is too old.  Need 0.2.2 or later not $verstr)
  fi
else
  AC_MSG_ERROR(can't run ROX-CLib)
fi

dnl Extract version info from AppInfo.xml
AC_MSG_CHECKING(version information)
changequote(,)dnl
VERSION=`sed -n 's/^.*<Version>\([^<]*\)<\/Version>.*$/\1/p' ../AppInfo.xml`
changequote([,])dnl
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_MSG_RESULT(version $VERSION)

AC_OUTPUT(Makefile)
