dnl Process this file with autoconf to produce a configure script.
AC_INIT(clock.c)
AC_CONFIG_HEADER(config.h)

AC_ARG_WITH(platform,
[  --with-platform	  platform name, as detected by AppRun],
[AC_SUBST(with_platform)],
[
AC_MSG_ERROR([

Please do not run 'configure' manually - instead, run the AppRun script with
the --compile option, like this:
	$ Clock/AppRun --compile

Thank you.
])
])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET

if test -z "$PKG_CONFIG"; then
  PKG_CONFIG=pkg-config
fi
AC_SUBST(PKG_CONFIG)

AC_MSG_CHECKING(that pkg-config runs)
if "$PKG_CONFIG" --version > /dev/null 2>&1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([*** $PKG_CONFIG missing ***

\'$PKG_CONFIG --version\' failed to run - make sure it is inside one of the
directories in your PATH environment variable!

pkg-config comes with the developement packages for GTK+-2.0, available at:

    http://www.gtk.org])
fi

AC_DEFUN(ROX_REQUIRE, [
AC_MSG_CHECKING(that $1 (version >= $2) is installed)
if "$PKG_CONFIG" --atleast-version=$2 $1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  if "$PKG_CONFIG" --exists $1 ; then
    AC_MSG_ERROR(Current version is only `"$PKG_CONFIG" "$1" --modversion`)
  else
    AC_MSG_ERROR(Package is not installed)
  fi
fi
])

ROX_REQUIRE(gtk+-2.0, 2.0.1)
ROX_REQUIRE(glib-2.0, 2.0.3)
ROX_REQUIRE(libxml-2.0, 2.4.0)

dnl Checks for header files.
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h locale.h sys/stat.h libintl.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(setlocale fstat stat bindtextdomain gettext)

dnl Check for ROX-CLib and version
ROX_CLIB(2, 1, 0)

dnl Is altzone defined in <time.h> ?
AC_MSG_CHECKING(for altzone)
AC_TRY_LINK(
#include <time.h>
,
int tmp; tmp=altzone;,
altzone=yes,
altzone=no
)
if test $altzone = yes; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(HAVE_ALTZONE)
else
  AC_MSG_RESULT(no)
fi

dnl Extract version info from AppInfo.xml
ROX_SELF_VERSION

AC_OUTPUT(Makefile)
