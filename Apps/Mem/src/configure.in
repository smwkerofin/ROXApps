dnl Process this file with autoconf to produce a configure script.
AC_INIT(mem.c)
AC_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET

dnl Forward support for GTK2
AC_ARG_WITH(gtk2,
[  --with-gtk2		  Use Gtk+-2.0 API instead of Gtk+-1.2], gtk2=1, gtk2=0)

if test -z "$PKG_CONFIG"; then
  PKG_CONFIG=pkg-config
fi
AC_SUBST(PKG_CONFIG)

if test -z "$GTK_CONFIG"; then
  GTK_CONFIG=gtk-config
fi
AC_SUBST(GTK_CONFIG)

if test "$gtk2" = "1"; then
  AC_MSG_WARN([Using EXPERIMENTAL Gtk+-2.0 API])
  AC_DEFINE(GTK2)
  gtk2=1
  GTK_CFLAGS="$PKG_CONFIG gtk+-2.0 --cflags"
  GTK_LIBS="$PKG_CONFIG gtk+-2.0 --libs"
else
  GTK_CFLAGS="$GTK_CONFIG --cflags"
  GTK_LIBS="$GTK_CONFIG --libs"
fi
AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)

if test -z "$ROX_RUN"; then
  AC_CHECK_PROG(ROX_RUN, rox_run, rox_run, "$APP_DIR/rox_run")
fi
AC_SUBST(ROX_RUN)

AC_CHECK_PROG(XML_CONFIG, xml2-config, xml2-config, false)
if test "$XML_CONFIG" != "false"; then
  AC_DEFINE(HAVE_XML)
fi

dnl Make sure we have libgtop
changequote(,)dnl
LIBGTOP_LIBS=`echo \`libgtop-config --libs --extra-libs\``
LIBGTOP_EXTRA_LIBS=`libgtop-config --extra-libs`

AC_MSG_CHECKING(for libgtop library)
if [ -n "$LIBGTOP_LIBS" ]; then
  AC_MSG_RESULT(yes - and with new style config)
  LIBGTOP_INCS=`libgtop-config --cflags`
else
  GTOP_CONFIG=`libgtop-config --config`
  if [ -n "$GTOP_CONFIG" ]; then
    AC_MSG_RESULT(yes - using '$GTOP_CONFIG')
  else
    AC_MSG_ERROR(can't run 'libgtop-config --config' - \
    		make sure it is in your PATH)
  fi
  source "$GTOP_CONFIG"
fi
changequote([,])dnl
AC_SUBST(LIBGTOP_EXTRA_LIBS)
AC_SUBST(LIBGTOP_LIBS)
AC_SUBST(LIBGTOP_INCS)

dnl Checks for header files.
AC_PATH_X
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h libintl.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(bindtextdomain gettext)

dnl Check for ROX-CLib and version
AC_MSG_CHECKING(ROX-CLib)
if $ROX_RUN ROX-CLib ; then
  verstr=`$ROX_RUN ROX-CLib -v`
  ver=`echo $verstr | cut -d " " -f 2`
  changequote(,)dnl
  ROX_CLIB_VERSION=`echo $ver | sed -e "s/\([0-9]*\)\.\([0-9]*\)\.\([0-9]*\)/10000*\1+100*\2+\3/" | bc`
  changequote([,])dnl
  AC_DEFINE_UNQUOTED(ROX_CLIB_VERSION, $ROX_CLIB_VERSION)
  AC_MSG_RESULT(version $ver)
  if test $ROX_CLIB_VERSION -lt 112 ; then
    AC_MSG_ERROR(ROX-CLib is too old.  Need 0.1.12 or later not $verstr)
  fi
else
  AC_MSG_ERROR(can't run ROX-CLib)
fi

dnl Extract version info from AppInfo.xml
AC_MSG_CHECKING(version information)
changequote(,)dnl
VERSION=`sed -n 's/^.*<Version>\([^>]*\)<\/Version>.*$/\1/p' ../AppInfo.xml`
changequote([,])dnl
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_MSG_RESULT(version $VERSION)

AC_OUTPUT(Makefile)
